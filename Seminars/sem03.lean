/-!
# Seminar 3
Quantifiers, tactics
-/

/- Universal Quantifier -/
variable (α : Type) (P Q : α → Prop)

#check ∀ (x : α), P x
#check ∀ x : α, P x
#check ∀ x, P x
#check ∀ x y : α, P x ∧ Q y
#check ∀ x : α, ∀ y : α, P x ∧ Q y
#check (x : α) → P x

example : (∀ x : α, P x) ∨ (∀ x : α, Q x) → ∀ x : α, P x ∨ Q x  :=
  λ h x ↦ Or.elim h (λ hP ↦ Or.inl (hP x)) (λ hQ ↦ Or.inr (hQ x))

example : (∀ (x : α), P x ∧ Q x) ↔ (∀ x : α, P x) ∧ (∀ x : α, Q x) :=
  sorry

/- Existential Quantifier -/

#check ∃ (x : α), P x
#check Exists P
#print Exists -- ?
#check Exists.intro

/- Why not this?
structure Exists' {α : Type} {P : α → Prop} : Prop where
  intro ::
  a : α
  b : P a
-/

example : (∃ x : α, P x ∧ Q x) → (∃ x : α, P x) ∧ (∃ x : α, Q x) :=
  λ ⟨x, hp, hq⟩ ↦ ⟨⟨x, hp⟩, ⟨x, hq⟩⟩

example : (∃ x, P x ∨ Q x) ↔ (∃ x, P x) ∨ (∃ x, Q x) :=
  sorry


/- Cantor theorem -/

/- 2^α ≃ α → Prop (at least classicaly) -/
theorem cantor (f : α → (α → Prop)) :
 ¬∀ S : α → Prop, ∃ x : α, ∀ y : α, S y ↔ f x y :=
  sorry

/- Classical logic -/

theorem not_exists_not : (¬ ∃ x : α, ¬ P x) → ∀ x : α, P x :=
  λ h x ↦ Or.elim (Classical.em (P x)) id (λ hn ↦ False.elim (h ⟨x, hn⟩))

theorem forall_or_exists_not : (∀ x : α, P x) ∨ (∃ x : α, ¬ P x) :=
  sorry

example (people : Type) (Joe : people) (drinks : people → Prop) :
  ∃ x : people, drinks x → ∀ y : people, drinks y :=
  sorry

/- Tactics -/

variable (p q r : Prop)

/- Intro, exact, apply, assumption -/

theorem ex1 : p → p := by
  intro hp
  exact hp

#print ex1

theorem ex2 : p → q → p := by
  intro hp
  intro hq
  exact hp

#print ex2

theorem ex3 : (p → q) → (q → r) → p → r := by
  intro hpq hqr hp
  exact (hqr (hpq hp))

#print ex3

theorem ex3' : (p → q) → (q → r) → p → r := by
  intro hpq hqr hp
  apply hqr
  apply hpq
  exact hp

#print ex3'

theorem ex4 : (p → (q → r)) → (p → q) → p → r := by
  intro hpqr hpq hp
  apply hpqr
  · exact hp
  · apply hpq
    apply hp

#print ex4

theorem ex5 : p ∧ (q ∨ r) → (p ∧ q) ∨ (p ∧ r) := by
  intro ⟨hp, hqr⟩
  apply Or.elim hqr
  . intro hq
    apply Or.inl
    apply And.intro
    . assumption
    . assumption
  . intro hr
    apply Or.inr
    apply And.intro
    . assumption
    . assumption

#print ex5

example : (p → q) → (¬ q → ¬ p) := by
  sorry

example : p ∧ q ↔ q ∧ p := by
  sorry

/- cases, constructor, left/right, exists -/

example : p ∨ q → q ∨ p := by
  intro hpq
  cases hpq
  case inl hp =>
    right
    assumption
  case inr =>
    left
    assumption

example : (p ∧ q) ∨ (p ∧ r) → p ∧ (q ∨ r) := by
  intro h
  cases h
  case inl hpq =>
    constructor
    · exact hpq.1
    · left
      apply hpq.2
  case inr hpr =>
    have ⟨hp, hr⟩ := hpr
    constructor
    · assumption
    · right
      assumption

example : p ∨ q → ¬ (¬ p ∧ ¬ q) := by
  sorry

example : (∀ x : α, P x) ∨ (∀ x : α, Q x) → ∀ x : α, P x ∨ Q x  := by
  intro h x
  cases h
  case inl h =>
    left
    apply h
  case inr h =>
    right
    apply h

example : (∃ x : α, P x ∧ Q x) → (∃ x : α, P x) ∧ (∃ x : α, Q x) := by
  intro ⟨x, hP, hQ⟩
  constructor
  · exists x
  · exists x

example : (∀ (x : α), P x ∧ Q x) ↔ (∀ x : α, P x) ∧ (∀ x : α, Q x) :=
  sorry

example : (∃ x, P x ∨ Q x) ↔ (∃ x, P x) ∨ (∃ x, Q x) :=
  sorry

/- Tactic combinators -/

example (p q : Prop) (hp : p) : p ∨ q := by
  apply Or.inl; assumption

example (p q : Prop) (hp : p) (hq : q) : p ∧ q :=
  by constructor <;> assumption

example (p q : Prop) (hp : p) : p ∨ q := by
  first | apply Or.inl; assumption | apply Or.inr; assumption

example (p q : Prop) (hq : q) : p ∨ q := by
  first | apply Or.inl; assumption | apply Or.inr; assumption

example (p q r : Prop) (hp : p) (hq : q) (hr : r) : p ∧ q ∧ r := by
  constructor <;> (try constructor) <;> assumption

example (p q r : Prop) (hp : p) (hq : q) (hr : r) :
  p ∧ ((p ∧ q) ∧ r) ∧ (q ∧ r ∧ p) := by
  repeat (any_goals constructor)
  all_goals assumption

example (p q r : Prop) (hp : p) (hq : q) (hr : r) :
  p ∧ ((p ∧ q) ∧ r) ∧ (q ∧ r ∧ p) := by
  repeat (any_goals (first | constructor | assumption))

example (p q r : Prop) (hp : p) :
  (p ∨ q ∨ r) ∧ (q ∨ p ∨ r) ∧ (q ∨ r ∨ p) := by
  sorry

/- Make your own tactic -/

-- Define a new tactic notation
syntax "triv" : tactic

macro_rules
  | `(tactic| triv) => `(tactic| assumption)

example (h : p) : p := by
  triv

macro_rules
  | `(tactic| triv) => `(tactic| rfl)

example (x : α) : x = x := by
  triv

example (x : α) (h : p) : x = x ∧ p := by
  apply And.intro <;> triv

macro_rules | `(tactic| triv) => `(tactic| constructor <;> triv)

example (x : α) (h : p) : x = x ∧ p := by
  triv

/-!
# Exercises
-/
section hw
  example : ∀ p q : Prop, p ∧ q ↔ ∀ r : Prop, (p → q → r) → r :=
    λ p q ↦ Iff.intro
      (λ ⟨hp, hq⟩ r h ↦ h hp hq)
      (λ h ↦ And.intro (h p (λ hp _ ↦ hp)) (h q (λ _ hq ↦ hq)))

  example : ∀ p q : Prop, p ∧ q ↔ ∀ r : Prop, (p → q → r) → r :=  by
    intro p q
    constructor
    · intro ⟨hp, hq⟩  r h
      apply h hp
      exact hq
    · intro h
      constructor
      · apply h p
        intro hp hq
        exact hp
      · apply h q
        intro hp hq
        exact hq


  example : ∀ p q : Prop, p ∨ q ↔ ∀ r : Prop, (p → r) → (q → r) → r :=
    λ p q ↦ Iff.intro
      (λ hpq r hpr hqr ↦
        Or.elim hpq
          (λ hp ↦ hpr hp)
          (λ hq ↦ hqr hq)
      )
      (λ h ↦ h (p ∨ q)
        (λ hp ↦ Or.inl hp)
        (λ hq ↦ Or.inr hq)
      )

  example : ∀ p q : Prop, p ∨ q ↔ ∀ r : Prop, (p → r) → (q → r) → r := by
    intro p q
    apply Iff.intro
    · intro hpq r hpr hqr
      apply Or.elim hpq
      · exact hpr
      · exact hqr
    · intro h
      apply h
      · intro hp
        apply Or.inl hp
      · intro hq
        apply Or.inr hq

  example : (∃ x : α, P x) ↔ ∀ r : Prop, (∀ x : α, P x → r) → r :=
    Iff.intro
      (λ ⟨x, hP⟩ r hpr ↦ (hpr x hP))
      (λ h ↦ h (∃ x : α, P x) (λ x hx ↦ ⟨x, hx⟩ ) )

  example : (∃ x : α, P x) ↔ ∀ r : Prop, (∀ x : α, P x → r) → r := by
    apply Iff.intro
    · intro ⟨x, hP⟩ r hpr
      apply hpr x hP
    · intro h
      apply h (∃ x : α, P x)
      intro x hx
      exact ⟨x, hx⟩


  theorem knaster_tarski
    (R : α → α → Prop) -- partial order relation
    (trans : ∀ x y z : α, R x y → R y z → R x z) -- transitivity
    (inf : ∀ P : α → Prop, -- subset definiton
      ∃ m : α,  -- existing of infimum
      (∀ x : α, P x → R m x) ∧ --lower_bound
      (∀ z : α, (∀ x : α, P x → R z x) → R z m)) --greatest_lower_bound
    (f : α → α) -- function on lattice
    (mono : ∀ x y : α, R x y → R (f x) (f y)) : -- monotonic function
    ∃ p : α, R (f p) p ∧ R p (f p) := by -- existing of fixed point
      let E := λ (x : α) ↦ R (f x) x -- set of x : f(x) ≤ x
      rcases inf E with ⟨m, ⟨lower_bound, greatest_lower_bound ⟩⟩ -- let m = inf E
      have h1 : R (f m) m := by -- show f(m) ≤ m
        apply greatest_lower_bound -- ∀ z : (∀ x ∈ E => z ≤ x) => z ≤ m
        intro x hx -- let x ∈ E i.e. f(x) ≤ x
        have hm_x := lower_bound x hx -- m ≤ x
        have hf_mono := mono m x hm_x -- f(m) ≤ f(x)
        exact trans (f m) (f x) x hf_mono hx -- f(m) ≤ f(x) => f(m) ≤ x => f(m) ≤ m
      have h2 : R m (f m) := by -- show m ≤ f(m)
        have h_fm_in_E : E (f m) := mono (f m) m h1 -- f(m) ∈ E
        exact lower_bound (f m) h_fm_in_E -- m ≤ f(m)
      exact ⟨m, ⟨h1, h2⟩⟩ -- (f(m) ≤ m) ∧ (m ≤ f(m)) => m = f(m)


  /-- Explain what this theorem means --/
  theorem girard
  -- конструкция, которая позволяет совпасть
  -- типу "функция из типа в тип" и типу аргумента
    (π : (Type → Type) → Type)
  -- это абстракция, которая, например, позволит ввести функцию (λx.xx)
    (Λ : ∀ {X : Type → Type}, ((α : Type) → X α) → π X)
  -- это применение (вычисление), которое, например,
  -- позволит применить функцию к себе (λx.xx)(λx.xx)
    (ε : ∀ {X : Type → Type}, π X → (α : Type) → X α)
  -- это β-редукция, которая, в результате, приведет к зацикливанию,
  -- то еcть выдаст False
    (β : ∀ {X : Type → Type} (f : (α : Type) → X α) (α : Type),
      ∀ (P : X α → Prop), P (ε (Λ f) α) ↔ P (f α))
    : False :=

    sorry
end hw
